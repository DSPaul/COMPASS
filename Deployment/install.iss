; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "COMPASS"
#define MyAppVersion "1.8.7"
#define MyAppPublisher "Paul De Smul"
#define MyAppURL "https://www.compassapp.info"
#define MyAppExeName "COMPASS.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{6BB9B60C-E9A6-4342-9E26-663ACBE88F2C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
ArchitecturesInstallIn64BitMode = x64compatible
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=Versions\{#MyAppVersion}
OutputBaseFilename=COMPASS_Setup_{#MyAppVersion}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
ChangesAssociations = yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\src\bin\Publish\win-x64\*"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\src\bin\Publish\win-x64\Media\*"; DestDir: "{app}\Media"; Flags: ignoreversion
Source: "..\src\bin\Publish\win-x64\gs\*"; DestDir: "{app}\gs"; Flags: ignoreversion
Source: "..\src\bin\Publish\win-x64\selenium-manager\windows\*"; DestDir : "{app}\selenium-manager\windows"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Registry]
; ----------- Register .satchel as a COMPASS file -------------- ;
; register the .sathel extension
Root: HKCR; Subkey: ".satchel"; ValueType: string; ValueName: ""; ValueData: "COMPASSSatchelFile"; Flags: uninsdeletevalue
; Give it a type name "COMPASS Satchel"
Root: HKCR; Subkey: "COMPASSSatchelFile"; ValueType: string; ValueName: ""; ValueData: "COMPASS Satchel"; Flags: uninsdeletekey
; Give it the COMPASS Icon
Root: HKCR; Subkey: "COMPASSSatchelFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\COMPASS.EXE,0"
; Make double click open the file using compass, not handled yet in COMPASS
; Root: HKCR; Subkey: "COMPASSSatchelFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\COMPASS.EXE"" ""%1"""
; -------------------------------------------------------------- ;

[Code]
procedure CurUninstallStepChanged (CurUninstallStep: TUninstallStep);
var
  mres : integer;
begin
  case CurUninstallStep of                   
    usPostUninstall:
      begin
        mres := MsgBox('Delete data on uninstall.', mbConfirmation, MB_YESNO or MB_DEFBUTTON2)
        if mres = IDYES then
          DelTree(ExpandConstant('{userappdata}\{#MyAppName}'), True, True, True);
      end;
  end;
end;